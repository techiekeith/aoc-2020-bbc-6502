HIMEM=&7000
NEW
AUTO
OSFIND=&FFCE
OSGBPB=&FFD1
OSBPUT=&FFD4
OSBGET=&FFD7
OSARGS=&FFDA
OSFILE=&FFDD
OSRDCH=&FFE0
OSASCI=&FFE3
OSNEWL=&FFE7
OSWRCH=&FFEE
start=&7800
src=&8C
dest=&8E
FOR N%=0 TO 2 STEP 2
    P%=start
    [
    OPT N%
    .set_integer_width_call
        JMP set_integer_width
    .add_call
        JMP add
    .subtract_call
        JMP subtract
    .multiply_call
        JMP multiply
    .divide_call
        JMP divide
    .print_string_call
        JMP print_string
    .print_decimal_call
        JMP print_decimal
    .print_hex_call
        JMP print_hex
    .print_hex_16bit_call
        JMP print_hex_16bit
    .print_hex_8bit_call
        JMP print_hex_8bit
    .counter
        EQUB 0
    .counter2
        EQUB 0
    .operand_offset
    .dividend
    .operand1
        EQUD 0
        EQUD 0
    .divisor
    .operand2
        EQUD 0
        EQUD 0
    .result
    .quotient
        EQUD 0
        EQUD 0
    .remainder
        EQUD 0
        EQUD 0
    .temp_number
        EQUD 0
        EQUD 0
    .integer_width
        EQUB 8

    .print_buffer
        EQUD 0
        EQUD 0
        EQUD 0
        EQUD 0
        EQUD 0
        EQUB 0

    .decimal_digit_lookup
        EQUB 3 ; 255
        EQUB 5 ; 65535
        EQUB 8 ; 16777215
        EQUB 10 ; 4294967295
        EQUB 13 ; 1099511627775
        EQUB 15 ; 281474976710655
        EQUB 17 ; 72057594037927936
        EQUB 20 ; 18446744073709551615

    .set_integer_width
        CMP #0
        BEQ invalid_integer_width
        CMP #9
        BCS invalid_integer_width
        STA integer_width
        RTS
    .invalid_integer_width
        BRK
        EQUB 255
        EQUS "Invalid integer_width; value must be between 1 and 8"
        BRK

	.print_string
		PLA
		STA src
		PLA
		STA src + 1
		LDY #1
	.print_string_loop
		LDA (src), Y
		BEQ print_string_return
		JSR OSWRCH
		INY
		BNE print_string_loop
	.print_string_return
		CLC
		TYA
		ADC src
		STA src
		LDA #0
		ADC src + 1
		PHA
		LDA src
		PHA
		RTS

	.print_hex_8bit
		PHA
		LSR A
		LSR A
		LSR A
		LSR A
		JSR print_digit
		PLA
		AND #15

	.print_digit
		ORA #48
		CMP #58
		BCC print_this_char
		ADC #6

	.print_this_char
		JMP OSWRCH

    .store_operand
        STX src
        STY src + 1
        ASL A
        ASL A
        ASL A
        ADC #operand_offset MOD 256
        STA dest
        LDA #operand_offset DIV 256
        STA dest + 1
        JMP copy_src_to_dest

    .load_operand
        STX dest
        STY dest + 1
        ASL A
        ASL A
        ASL A
        ADC #operand_offset MOD 256
        STA src
        LDA #operand_offset DIV 256
        STA src + 1
    
    .copy_src_to_dest
        LDY #0
    .copy_src_to_dest_loop
        LDA (src), Y
        STA (dest), Y
        INY
        CPY integer_width
        BNE copy_src_to_dest_loop
        RTS

    .add
        LDY #0
        CLC
        PHP
    .add_loop
        PLP
		LDA operand1, Y
		ADC operand2, Y
		STA result, Y
        PHP
        INY
        CPY integer_width
        BNE add_loop
        PLP
        RTS

    .subtract
        LDY #0
        SEC
        PHP
    .subtract_loop
        PLP
		LDA operand1, Y
		SBC operand2, Y
		STA result, Y
        PHP
        INY
        CPY integer_width
        BNE subtract_loop
        PLP
        RTS

    .lsr_remainder
        LDX integer_width
        DEX
        LSR remainder, X
        PHP
        CPX #0
        BEQ lsr_remainder_end
    .lsr_remainder_loop
        PLP
        DEX
        ROR remainder, X
        PHP
        CPX #0
        BNE lsr_remainder_loop
    .lsr_remainder_end
        PLP
        RTS

    .asl_dividend
        ASL dividend
        PHP
        LDX #1
        CPX integer_width
        BEQ asl_dividend_end
    .asl_dividend_loop
        PLP
        ROL dividend, X
        PHP
        INX
        CPX integer_width
        BNE asl_dividend_loop
    .asl_dividend_end
        PLP
        RTS

    .asl_temp_number
        ASL temp_number
        PHP
        LDX #1
        CPX integer_width
        BEQ asl_temp_number_end
    .asl_temp_number_loop
        PLP
        ROL temp_number, X
        PHP
        INX
        CPX integer_width
        BNE asl_temp_number_loop
    .asl_temp_number_end
        PLP
        RTS

    .rol_quotient
        PHP
        LDX #0
    .rol_quotient_loop
        PLP
        ROL quotient, X
        PHP
        INX
        CPX integer_width
        BNE rol_quotient_loop
        PLP
        RTS

    .rol_remainder
        PHP
        LDX #0
    .rol_remainder_loop
        PLP
        ROL remainder, X
        PHP
        INX
        CPX integer_width
        BNE rol_remainder_loop
        PLP
        RTS

    .zero_operand
        ASL A
        ASL A
        ASL A
        ADC #operand_offset MOD 256
        STA dest
        LDA #operand_offset DIV 256
        STA dest + 1
		LDA #0
		TAY
    .zero_operand_loop
		STA (dest), Y
        INY
        CPY integer_width
        BNE zero_operand_loop
        RTS

	.multiply
        LDA #0 ; operand1
        LDX #temp_number MOD 256
        LDY #temp_number DIV 256
        JSR load_operand ; operand1 -> temp_number
        LDA #1 ; operand2
        LDX #remainder MOD 256
        LDY #remainder DIV 256
        JSR load_operand ; operand2 -> remainder
        LDA #2 ; result
        JSR zero_operand ; result <- 0
        LDA integer_width
        ASL A
        ASL A
        ASL A
        STA counter ; number of bits to rotate
	.multiply_loop
        LDA #1 ; operand2
        LDX #result MOD 256
        LDY #result DIV 256
        JSR store_operand ; operand2 <- result
        JSR lsr_remainder
		BCC multiply_loop_end
        LDA #0 ; operand1
        LDX #temp_number MOD 256
        LDY #temp_number DIV 256
        JSR store_operand ; operand1 <- temp_number
		JSR add
	.multiply_loop_end
        JSR asl_temp_number
        DEC counter
		BNE multiply_loop
		RTS

	.divide
        LDA #2 ; quotient
        JSR zero_operand ; quotient <- 0
        LDA #3 ; remainder
        JSR zero_operand ; remainder <- 0
        LDA integer_width
        ASL A
        ASL A
        ASL A
        STA counter ; number of bits to rotate
	.divide_loop
        JSR asl_dividend
		JSR rol_remainder
        LDX #0
		SEC
        PHP
    .divide_subtract_loop
		LDA remainder, X
        PLP
		SBC divisor, X
        PHP
		STA temp_number, X
        INX
        CPX integer_width
        BNE divide_subtract_loop
        PLP
        DEX
        LDA temp_number, X
        BMI divide_next
        LDA #3 ; remainder
        LDX #temp_number MOD 256
        LDY #temp_number DIV 256
        JSR store_operand ; remainder <- temp_number
		SEC
	.divide_next
		JSR rol_quotient
        DEC counter
		BNE divide_loop
		RTS
    
    .print_hex
        STX src
        STY src + 1
        LDY integer_width
    .print_hex_loop
        DEY
        LDA (src), Y
        JSR print_hex_8bit
        CPY #0
        BNE print_hex_loop
        RTS
    
    .print_decimal
        STX src
        STY src + 1
        LDX #dividend MOD 256
        LDY #dividend DIV 256
        STX dest
        STY dest + 1
        JSR copy_src_to_dest
        LDA #1 ; divisor
        JSR zero_operand
        LDA #10
        STA divisor
        LDX integer_width
        DEX
        LDA decimal_digit_lookup, X
        STA counter2
        TAX
        LDA #0
        STA print_buffer, X
        DEX
        LDA #32
    .init_decimal_buffer_loop
        STA print_buffer, X
        DEX
        BPL init_decimal_buffer_loop
        LDA integer_width
    .fill_decimal_buffer_loop
        JSR divide
        LDA #0 ; dividend
        LDX #result MOD 256
        LDY #result DIV 256
        JSR store_operand ; dividend <- result
        LDA remainder
        ORA #48
        DEC counter2
        LDX counter2
        STA print_buffer, X
        LDA #0
        TAY
    .test_quotient
        ORA quotient, Y
        INY
        CPY integer_width
        BNE test_quotient
        CMP #0
        BEQ output_decimal
        CPX #0
        BNE fill_decimal_buffer_loop
    .output_decimal
        LDX #0
    .output_decimal_loop
        LDA print_buffer, X
        BEQ output_decimal_end
        CMP #32
        BEQ output_decimal_skip
        JSR OSWRCH
    .output_decimal_skip
        INX
        BNE output_decimal_loop
    .output_decimal_end
        RTS

    .print_hex_16bit
        TYA
        JSR print_hex_8bit
        TXA
        JMP print_hex_8bit

    .run
        LDA #8
        JMP set_integer_width
    .end
    ]
NEXT
PRINT ;~P%
END

DEF PROCtest
?integer_width=8

B%=&A5288000
C%=&746
D%=&52944000
E%=&3A3
X%=8
Y%=4
CALL print_decimal
PRINT " + ";
X%=&10
Y%=4
CALL print_decimal
PRINT " = ";
!operand1 = B%
!(operand1 + 4) = C%
!operand2 = D%
!(operand2 + 4) = E%
CALL add
F%=!result
G%=!(result + 4)
X%=&18
Y%=4
CALL print_decimal
PRINT

X%=&18
Y%=4
CALL print_decimal
PRINT " - ";
X%=&10
Y%=4
CALL print_decimal
PRINT " = ";
!operand1 = F%
!(operand1 + 4) = G%
!operand2 = D%
!(operand2 + 4) = E%
CALL subtract
X%=result MOD 256
Y%=result DIV 256
CALL print_decimal
PRINT

?integer_width = 8

!operand1 = 4000000
!(operand1 + 4) = 0
!operand2 = 2000000
!(operand2 + 4) = 0
PRINT STR$(!operand1); " x ";STR$(!operand2);" = ";
CALL multiply
X%=result MOD 256
Y%=result DIV 256
CALL print_decimal
PRINT

!operand1 = 4000000
!(operand1 + 4) = 0
!operand2 = 2000000
!(operand2 + 4) = 0
PRINT STR$(!operand1); " ~ ";STR$(!operand2);" = ";
CALL divide
X%=result MOD 256
Y%=result DIV 256
CALL print_decimal
PRINT

B%=&A5288000
C%=&746
X%=8
Y%=4
CALL print_decimal
!operand1 = B%
!(operand1 + 4) = C%
!operand2 = 2000000
!(operand2 + 4) = 0
PRINT " ~ ";STR$(!operand2);" = ";
CALL divide
X%=result MOD 256
Y%=result DIV 256
CALL print_decimal
PRINT

?integer_width=4

!operand1 = 10
!operand2 = 2
PRINT STR$(!operand1);" ~ ";STR$(!operand2);" = ";
CALL divide
X%=result MOD 256
Y%=result DIV 256
CALL print_decimal
PRINT
ENDPROC

DEF PROCsave
A%=0
B%=&41C0000
C%=start
D%=run
E%=start
F%=end
X%=10
Y%=4
$&41C="L.MATH"
CALL OSFILE
ENDPROC
